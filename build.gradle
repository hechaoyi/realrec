subprojects {
	apply plugin: "java"
	apply plugin: "eclipse"
	version = "1.0-SNAPSHOT"
	sourceCompatibility = "1.7"
	ext {
		mybatisVer = "3.2.1"
		mysqlVer   = "5.1.24"
		nettyVer   = "4.0.0.Beta2"
		bdbjeVer   = "5.0.73"
		guavaVer   = "14.0"
		logbackVer = "1.0.9"
		slf4jVer   = "1.7.2"
	}
	repositories {
		mavenCentral()
		maven { url "http://clojars.org/repo/" }
		maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
		maven { url "http://download.oracle.com/maven/" }
	}
	configurations {
		compile.extendsFrom provided
	}
	jar.from {
		(configurations.runtime-configurations.provided).collect { it.isDirectory() ? it : zipTree(it) }
	}
	eclipse.classpath.noExportConfigurations += configurations.provided
	eclipse.classpath.defaultOutputDir = file("$buildDir/output")
}

project(":realrec-common") {
	dependencies {
		compile "io.netty:netty-codec:$nettyVer"
		compile "org.slf4j:slf4j-api:$slf4jVer"
	}
}

project(":cbox-storm-v1") {
	apply plugin: "application"
	dependencies {
		compile project(":realrec-common")
		provided "storm:storm:0.8.2"
		provided "com.google.guava:guava"
		compile  "org.mybatis:mybatis:$mybatisVer"
		runtime  "mysql:mysql-connector-java:$mysqlVer"
	}
	run {
		main = "realrec.cbox.storm.driver.LocalTopology"
		systemProperties["metadata.hosts"] = "macbookair:5587"
	}
	jar.manifest.attributes["Main-Class"] = "realrec.cbox.storm.driver.RemoteTopology"
}

project(":cbox-metadata") {
	apply plugin: "application"
	dependencies {
		compile project(":realrec-common")
		compile "com.sleepycat:je:$bdbjeVer"
		compile "com.google.guava:guava:$guavaVer"
		compile "ch.qos.logback:logback-classic:$logbackVer"
	}
	mainClassName = "realrec.cbox.metadata.server.MetaDataServer"
	jar.manifest.attributes["Main-Class"] = mainClassName
}
