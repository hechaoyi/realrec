subprojects {
	apply plugin: "java"
	apply plugin: "eclipse"
	version = "1.0-SNAPSHOT"
	sourceCompatibility = "1.7"
	ext {
		nettyVer   = "4.0.0.Beta3"
		slf4jVer   = "1.7.2"
		jacksonVer = "2.1.4"
		bdbjeVer   = "5.0.73"
		guavaVer   = "14.0.1"
		logbackVer = "1.0.10"
		stormVer   = "0.8.2"
		mybatisVer = "3.2.1"
		mysqlVer   = "5.1.24"
	}
	repositories {
		mavenCentral()
		maven { url "http://clojars.org/repo/" }
		maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
		maven { url "http://download.oracle.com/maven/" }
	}
	configurations {
		compile.extendsFrom provided
	}
	jar.from {
		(configurations.runtime-configurations.provided).collect { it.isDirectory() ? it : zipTree(it) }
	}
	eclipse.classpath.noExportConfigurations += configurations.provided
	eclipse.classpath.defaultOutputDir = file("$buildDir/output")
}

project(":realrec-common") {
	dependencies {
		compile "io.netty:netty-codec:$nettyVer"
		compile "org.slf4j:slf4j-api:$slf4jVer"
		compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
		compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVer"
	}
}

project(":cbox-metadata") {
	apply plugin: "application"
	dependencies {
		compile project(":realrec-common")
		compile "com.sleepycat:je:$bdbjeVer"
		compile "com.google.guava:guava:$guavaVer"
		compile "ch.qos.logback:logback-classic:$logbackVer"
	}
	mainClassName = "realrec.cbox.metadata.server.MetaDataServer"
	jar.manifest.attributes["Main-Class"] = mainClassName
}

project(":cbox-storm-v1") {
	apply plugin: "application"
	dependencies {
		compile project(":realrec-common")
		provided "storm:storm:$stormVer"
		provided "com.google.guava:guava"
		compile  "org.mybatis:mybatis:$mybatisVer"
		runtime  "mysql:mysql-connector-java:$mysqlVer"
	}
	run {
		main = "realrec.cbox.storm.driver.LocalTopology"
		systemProperties["metadata.hosts"] = "macbookair:5587"
	}
	jar.manifest.attributes["Main-Class"] = "realrec.cbox.storm.driver.RemoteTopology"
}
