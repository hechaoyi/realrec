subprojects {
	apply plugin: "java"
	apply plugin: "eclipse"
	version = "1.0-SNAPSHOT"
	sourceCompatibility = "1.7"
	ext {
		nettyVer   = "4.0.0.CR1"
		slf4jVer   = "1.7.5"
		jacksonVer = "2.1.4"
		bdbjeVer   = "5.0.73"
		guavaVer   = "14.0.1"
		logbackVer = "1.0.11"
		stormVer   = "0.8.2"
		mybatisVer = "3.2.2"
		mysqlVer   = "5.1.24"
		hbaseVer   = "0.94.2-cdh4.2.0"
		hadoopVer  = "2.0.0-cdh4.2.0"
		myrrixVer  = "0.11"
		jerseyVer  = "1.17.1"
		springVer  = "3.2.2.RELEASE"
	}
	repositories {
		mavenCentral()
		maven { url "http://clojars.org/repo/" }
		maven { url "http://download.oracle.com/maven/" }
		maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
	}
	configurations {
		compile.extendsFrom provided
	}
	task onejar(type: Jar, dependsOn: classes) {
		from {
			def list = []
			def result = (configurations.runtime-configurations.provided).collect {
				list.add(it.name)
				it.isDirectory() ? it : zipTree(it)
			}
			gradle.taskGraph.whenReady { graph ->
				if(graph.hasTask(onejar)) println "${project.name} deps in jar: ${list.unique().sort()}"
			}
			result + sourceSets.main.output
		}
	}
	eclipse.classpath.noExportConfigurations += configurations.provided
	eclipse.classpath.defaultOutputDir = file("$buildDir/output")
}

project(":realrec-common-v1") {
	dependencies {
		compile "io.netty:netty-codec:$nettyVer"
		compile "org.slf4j:slf4j-api:$slf4jVer"
		compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
		compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVer"
	}
}

project(":cbox-metadata-v1") {
	apply plugin: "application"
	dependencies {
		compile project(":realrec-common-v1")
		compile "com.sleepycat:je:$bdbjeVer"
		compile "com.google.guava:guava:$guavaVer"
		compile "ch.qos.logback:logback-classic:$logbackVer"
		compile "org.mybatis:mybatis:$mybatisVer"
		runtime "mysql:mysql-connector-java:$mysqlVer"
	}
	mainClassName = "realrec.cbox.metadata.server.MetaDataServer"
	onejar.manifest.attributes["Main-Class"] = mainClassName
}

project(":cbox-storm-v1") {
	apply plugin: "application"
	dependencies {
		compile  project(":realrec-common-v1")
		provided "storm:storm:$stormVer"
		provided "com.google.guava:guava"
		provided "org.slf4j:slf4j-log4j12:$slf4jVer"
		compile  "org.mybatis:mybatis:$mybatisVer"
		runtime  "mysql:mysql-connector-java:$mysqlVer"
		compile  "net.myrrix:myrrix-client:$myrrixVer"
		// HBase
		compile "org.apache.hbase:hbase:$hbaseVer", { transitive = false }
		compile "com.google.protobuf:protobuf-java:2.4.0a", { transitive = false }
		compile "commons-configuration:commons-configuration:1.6", { transitive = false }
		compile "org.apache.hadoop:hadoop-common:$hadoopVer", { transitive = false }
		compile "org.apache.hadoop:hadoop-auth:$hadoopVer", { transitive = false }
	}
	mainClassName = "realrec.cbox.storm.driver.LocalTopology"
	onejar.manifest.attributes["Main-Class"] = "realrec.cbox.storm.driver.RemoteTopology"
}

project(":cbox-restful-v1") {
	apply plugin: "jetty"
	dependencies {
		compile project(":realrec-common-v1")
		compile "javax.ws.rs:jsr311-api:1.1.1"
		compile "com.sun.jersey:jersey-core:$jerseyVer"
		compile "com.sun.jersey:jersey-server:$jerseyVer"
		compile "com.sun.jersey:jersey-servlet:$jerseyVer"
		compile "com.sun.jersey:jersey-json:$jerseyVer"
		compile "com.sun.jersey.contribs:jersey-spring:$jerseyVer"
		compile "org.springframework:spring-web:$springVer"
		compile "net.myrrix:myrrix-client:$myrrixVer"
	}
}
